##
# (c) 2024 - Cloud Ops Works LLC - https://cloudops.works/
#            On GitHub: https://github.com/cloudopsworks
#            Distributed Under Apache v2.0 License
#

# Entry Format:
#
# name_prefix: string
# description: string
# assume_roles:
#   - actions: list(string)
#     type: string
#     principals: list(string)
#     principal_refs: list(string)
#     conditions:
#       - test: string
#         values: list(string)
#         variable: string
# managed_policies: list(string)
# policy_refs: list(string)
# inline_policies:
#   - name: string
#     statements:
#       - sid: string
#         effect: string
#         actions: list(string)
#         resources: list(string)
#         resource_refs: list(string)
#         conditions:
#           - test: string
#             values: list(string)
#             variable: string
locals {
  roles_map = { for role in var.roles : role.name_prefix => role }
  instance_profile_map = {
    for name, role in local.roles_map : name => role
    if try(role.instance_profile, false) == true
  }
  managed_policies = merge(
    [
      for role in var.roles : {
        for policy_arn in try(role.managed_policies, []) : "${role.name_prefix}-${policy_arn}" => {
          name_prefix = role.name_prefix
          policy_arn  = policy_arn
        }
      }
  ]...)
  policy_refs = merge(
    [
      for role in var.roles : {
        for policy_ref in try(role.policy_refs, []) : "${role.name_prefix}-${policy_ref}" => {
          name_prefix = role.name_prefix
          policy_ref  = policy_ref
        }
      }
    ]...
  )
  inline_policies = merge(
    [
      for role in var.roles : {
        for policy in try(role.inline_policies, []) : "${role.name_prefix}-${policy.name}" => {
          name_prefix = role.name_prefix
          name        = policy.name
          statements = [
            for statement in policy.statements :
            statement if length(try(statement.resources, [])) > 0
          ]
        }
      }
  ]...)
  inline_policies_refs = merge(
    [
      for role in var.roles : {
        for policy in try(role.inline_policies, []) : "${role.name_prefix}-${policy.name}-refs" => {
          name_prefix = role.name_prefix
          name        = policy.name
          statements = [
            for statement in policy.statements :
            statement if length(try(statement.resource_refs, [])) > 0
          ]
        }
      }
  ]...)

  assume_role_principals = {
    for role in var.roles : role.name_prefix => {
      name_prefix = role.name_prefix
      statements = try(role.assume_roles, [
        {
          actions    = ["sts:AssumeRole"]
          type       = "Service"
          principals = ["ec2.amazonaws.com"]
          conditions = []
        }
      ])
    }
  }
}

# STS Assume
data "aws_iam_policy_document" "assume_role" {
  for_each = local.assume_role_principals
  version  = "2012-10-17"
  dynamic "statement" {
    for_each = each.value.statements
    content {
      effect  = "Allow"
      actions = statement.value.actions
      principals {
        type        = statement.value.type
        identifiers = statement.value.principals
        # identifiers = concat(try(statement.value.principals, []), [
        #   for item in statement.value.principal_refs :
        #   aws_iam_role.this[item].arn
        # ])
      }
      dynamic "condition" {
        for_each = try(statement.value.conditions, [])
        content {
          test     = condition.value.test
          values   = condition.value.values
          variable = condition.value.variable
        }
      }
    }
  }
}

# The role itself
resource "aws_iam_role" "this" {
  for_each           = local.roles_map
  name               = "${each.value.name_prefix}-${local.system_name}"
  path               = try(each.value.path, null)
  assume_role_policy = data.aws_iam_policy_document.assume_role[each.key].json
  description        = try(each.value.description, "") != "" ? each.value.description : "IAM Role ${each.value.name_prefix}-${local.system_name}"
  #   managed_policy_arns = toset(try(each.value.managed_policies, {}))
  tags = local.all_tags
}

data "aws_iam_policy" "managed" {
  for_each = local.managed_policies
  arn      = each.value.policy_arn
}

resource "aws_iam_role_policy_attachment" "managed" {
  for_each   = local.managed_policies
  role       = aws_iam_role.this[each.value.name_prefix].name
  policy_arn = data.aws_iam_policy.managed[each.key].arn
}

# Inline policies
data "aws_iam_policy_document" "inline" {
  for_each = local.inline_policies
  version  = "2012-10-17"
  dynamic "statement" {
    for_each = each.value.statements
    content {
      sid       = try(statement.value.sid, null)
      effect    = statement.value.effect
      actions   = statement.value.actions
      resources = statement.value.resources
      dynamic "condition" {
        for_each = try(statement.value.conditions, [])
        content {
          test     = condition.value.test
          values   = condition.value.values
          variable = condition.value.variable
        }
      }
    }
  }
}

data "aws_iam_policy_document" "inline_refs" {
  for_each = local.inline_policies_refs
  version  = "2012-10-17"
  dynamic "statement" {
    for_each = each.value.statements
    content {
      sid     = try(statement.value.sid, null)
      effect  = statement.value.effect
      actions = statement.value.actions
      resources = [
        for item in try(statement.value.resource_refs, []) :
        aws_iam_role.this[item].arn
      ]
      dynamic "condition" {
        for_each = try(statement.value.conditions, [])
        content {
          test     = condition.value.test
          values   = condition.value.values
          variable = condition.value.variable
        }
      }
    }
  }
}

resource "aws_iam_role_policy" "inline" {
  for_each = local.inline_policies
  name     = each.value.name
  role     = aws_iam_role.this[each.value.name_prefix].id
  policy   = data.aws_iam_policy_document.inline[each.key].json
}

resource "aws_iam_role_policy" "inline_refs" {
  for_each = local.inline_policies_refs
  name     = each.value.name
  role     = aws_iam_role.this[each.value.name_prefix].id
  policy   = data.aws_iam_policy_document.inline_refs[each.key].json
}

resource "aws_iam_role_policy_attachment" "policy_ref" {
  for_each   = local.policy_refs
  role       = aws_iam_role.this[each.value.name_prefix].id
  policy_arn = aws_iam_policy.this[each.value.policy_ref].arn
}

resource "aws_iam_instance_profile" "this" {
  for_each = local.instance_profile_map
  name     = "${each.value.name_prefix}-${local.system_name}"
  role     = aws_iam_role.this[each.key].name
}